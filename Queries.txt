# Konstruiere adressen.ttl 

PREFIX leo: <http://le-online.de/ontology/place/ns#> 
construct { 
  ?a a leo:Place; leo:address ?b; leo:fixedAddress ?b; 
  leo:placeName ?n; leo:fixedPlaceName ?n . 
}
where {
?a leo:address ?b; leo:placeName ?n .
}

?a ist dabei eine HGG-URI. Später wurden noch KA-URIs mit owl:sameAs
hinzugefügt. 

Analyse:

PREFIX leo: <http://le-online.de/ontology/place/ns#> 
select count(distinct ?a) 
from <http://le-online.de/Test/>
where {
?a leo:address ?b .
}

-- 1390 Einträge, durch URI-Clash fallen davon allerdings mehrere zusammen:

PREFIX leo: <http://le-online.de/ontology/place/ns#> 
select ?a count(?b) as ?c
from <http://le-online.de/Test/>
where {
?a leo:address ?b .
} group by ?a
having count(?b)>1
order by desc(?c)

-- alle 1390 Einträge haben Zuordnung owl:sameAs Abicht-URI
-- 1304 Einträge haben Zuordnung leo:ldAddress 

PREFIX leo: <http://le-online.de/ontology/place/ns#> 
select count(distinct ?a) 
from <http://le-online.de/Test/>
where {
?a leo:address ?b .
optional { ?a leo:ldAddress ?ld . }
optional { ?a owl:sameAs ?ka . }
filter (!bound(?ld))
}

-- finde die 86 Einträge ohne leo:ldAddress 

PREFIX leo: <http://le-online.de/ontology/place/ns#> 
select distinct ?a
from <http://le-online.de/Test/>
where {
?a leo:address ?b .
optional { ?a leo:ldAddress ?ld . }
optional { ?a owl:sameAs ?ka . }
filter (!bound(?ld))
}

# ------------------------------------------------------

# Extrahiere die LD Adressen als URI Liste

PREFIX leo: <http://le-online.de/ontology/place/ns#> 
select distinct ?b
from <http://le-online.de/Test/>
where { ?a leo:ldAddress ?b . }

und stecke das in LD/Tools/PHP/getLDAdressen.php, um dazu bekannte
Informationen über Adressen und Geokoordinaten aus LeipzigData zu extrahieren.
Aktuell ein Timeout bei einer Anfrage.

Alternativ: 

PREFIX leo: <http://le-online.de/ontology/place/ns#> 
PREFIX ld: <http://leipzig-data.de/Data/Model/> 
construct { ?b a leo:Adresse . }
from <http://le-online.de/Test/>
where { ?a leo:ldAddress ?b . }
-- 933 Adressen

Gespeichert unter ld-adressen.ttl. Lade das als <http://le-online.de/places/>
in den RDF-Store unter leipzig-data.de/Data und frage den SPARQL-Endpunkt
http://leipzig-data.de:8890/sparql

PREFIX leo: <http://le-online.de/ontology/place/ns#> 
PREFIX ogcgs: <http://www.opengis.net/ont/geosparql#> 
select distinct ?a xsd:string(?l) xsd:string(?g)
from <http://le-online.de/places/>
from <http://leipzig-data.de/Data/Adressen/>
from <http://leipzig-data.de/Data/GeoDaten/>
where {
?a a leo:Adresse ; rdfs:label ?l .
optional { ?a ogcgs:asWKT ?g . } 
}
-- 707 Adressen

Extrahiere das und reichere damit sowie mit den Ortsteilinformationen
ld-adressen.ttl an.

PREFIX leo: <http://le-online.de/ontology/place/ns#> 
PREFIX ld: <http://leipzig-data.de/Data/Model/> 
PREFIX ogcgs: <http://www.opengis.net/ont/geosparql#> 
construct { ?a rdfs:label ?l; ld:inOrtsteil ?o; ogcgs:asWKT ?g . }
from <http://le-online.de/places/>
from <http://leipzig-data.de/Data/Adressen/>
from <http://leipzig-data.de/Data/GeoDaten/>
where {
?a a leo:Adresse ; ld:inOrtsteil ?o; rdfs:label ?l  .
optional { ?a ogcgs:asWKT ?g . } 
}

# ------------------------------------------------------
Analyse der Datei leo-original-20160725.nt

Lade das als <http://le-online.de/Test/> in einen lokalen RDF-Store

-- Zähle alle verschiedenen Subjekte 
PREFIX leo: <http://le-online.de/ontology/place/ns#> 
select count(distinct ?a) 
from <http://le-online.de/Test/>
where {
?a ?b ?c .
}

926 Einträge - davon einer <http://le-online.de/Test/>

-- Zähle alle Einträge mit Geokoordinaten
PREFIX leo: <http://le-online.de/ontology/place/ns#> 
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#> 
select count(?a)
from <http://le-online.de/Test/>
where {
?a  geo:latitude ?lat ; geo:long ?long .
}

1018 Einträge

-- Gibt es Einträge ohne Geokoordinaten?
PREFIX leo: <http://le-online.de/ontology/place/ns#> 
select ?a  
from <http://le-online.de/Test/>
where {
?a ?b ?c .
optional { ?a  geo:latitude ?lat . }
filter (!bound(?lat))
}

nur <http://le-online.de/Test/>

-- Einträge mit mehreren Koordinatenangaben
PREFIX leo: <http://le-online.de/ontology/place/ns#> 
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#> 
select ?a
from <http://le-online.de/Test/>
where {
?a  geo:latitude ?lat .
}
GROUP by ?a
HAVING( count(?lat)>1)

http://le-online.de/place/voelkerschlachtdenkmal 	2
http://le-online.de/place/polizeidirektion-leipzig 	4
http://le-online.de/place/park-krankenhaus-leipzig-suedost-gmbh 	2
http://le-online.de/place/komm-haus 	2
http://le-online.de/place/sozialstation-gruenau 	3
http://le-online.de/place/staedtisches-bestattungswesen---eigenbetrieb 	3
http://le-online.de/place/first-reisebuero 	3
http://le-online.de/place/auslaenderbehoerde 	2
http://le-online.de/place/der-paritaetische-sachsen-e.v. 	2
http://le-online.de/place/servicestelle-fuer-rehabilitation 	2
http://le-online.de/place/sozialstation-holzhausen 	2
http://le-online.de/place/wolf---das-sanitaetshaus 	4
http://le-online.de/place/lesti---sanitaetshaus---orthopaedie--und-rehatechnik 	2
http://le-online.de/place/merkurpromenade 	2
http://le-online.de/place/awo-werkstaetten-sachsen-west-wfb 	2
http://le-online.de/place/hauptbahnhof 	2
http://le-online.de/place/augustusplatz 	2
http://le-online.de/place/stadtwerke-leipzig 	2

-- Geokoordinaten als ogcgs:asWKT ausgeben
PREFIX leo: <http://le-online.de/ontology/place/ns#> 
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#> 
select ?a concat("ogcgs:asWKT Point(",?long," ",?lat,")")
from <http://le-online.de/Test/>
where {
?a  geo:latitude ?lat ; geo:long ?long .
} order by ?a




PREFIX leo: <http://le-online.de/ontology/place/ns#> 
select ?a ?ka ?hgg concat("Adresse(",?strasse,",",?plz,",",?ort,")") concat("Point(",?long," ",?lat,")")
from <http://le-online.de/place/HGG-Datenextrakt/>
where {
?a  a leo:Place ; leo:KAId ?ka; leo:HGGId ?hgg .
optional { ?a leo:Strasse ?strasse; leo:PLZ ?plz; leo:Ort ?ort . }
optional { ?a leo:Longitude ?long; leo:Latitude ?lat . }
}
